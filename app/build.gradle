plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
    id 'com.squareup.leakcanary.deobfuscation'
}

leakCanary {
    // LeakCanary needs to know which variants have obfuscation turned on
    filterObfuscatedVariants { variant ->
        variant.name == "debug"
    }
}

apply from: '../shared_dependencies.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "monster.myapp.moviecatalogue"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField("String", "IMG_URL", "\"https://image.tmdb.org/t/p/w500\"")
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dynamicFeatures = [':favorite']

}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation project(":core")
    //kotlin
    implementation ("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"){
        exclude group:"org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //ui
    implementation "de.hdodenhof:circleimageview:$circleImgVersion"
    implementation "com.github.skydoves:androidveil:1.1.1"
    implementation 'androidx.preference:preference-ktx:1.1.1'

    //navigation
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
}